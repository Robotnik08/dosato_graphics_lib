include "graphics.to"

make bool running = true

const window = createWindow(100, 1000, 1000, window_flags->WINDOW_FULLSCREEN_DESKTOP)

const sprite = createSprite(window, "elephant.png")

const spriteSize = getSpriteSize(sprite)

make double x = 0
make double y = 0

make long lastTime = time()
make long clockTime = 0
make long fps = 0

make double dt = 0.0

make double speed = 10.0

do {
    do updateState()

    set running = false when (ifQuit())

    const mouse_pos = getMousePosition()

    do setColor(window, 255, 0, 0, 255) when (getMouse(keys->MiddleMouse)) else setColor(window, 0, 255, 0, 255)
    do drawSprite(window, sprite, x - spriteSize->width/2, y - spriteSize->height/2, spriteSize->width, spriteSize->height)

    do drawLine(window, mouse_pos->x, mouse_pos->y, x, y)
    
    set x -= speed * dt when (getKey(keys->KeyA))
    set x += speed * dt when (getKey(keys->KeyD))
    set y += speed * dt when (getKey(keys->KeyS))
    set y -= speed * dt when (getKey(keys->KeyW))

    set x = clamp(x, 0, 1000)
    set y = clamp(y, 0, 1000)

    do render(window)
    do setColor(window, 0, 0, 0, 255)
    do clearWindow(window)

    set fps++

    do if (time() != lastTime) then {
        do sayln("FPS: " + fps)
        set fps = 0
        set lastTime = time()
    }
    
    set dt = (clock() - clockTime) / (1000.0 / 60.0)
    set clockTime = clock()
} while (running)

do closeWindow(window)